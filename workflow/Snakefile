import os
import sys
import math
from snakemake.utils import min_version

min_version("6.0")

SDIR = os.path.realpath(os.path.dirname(srcdir("Snakefile")))
shell.prefix(f"set -eo pipefail; ")

configfile: "config/config.yaml"

config["temp"] = config.get("temp", "temp")
config["tech"] = config.get("tech", "nanopore-raw")
config["threads"] = config.get("threads", 40)
config["genomeSize"] = config.get("genomeSize", "3g")
print(config)


rule collect_reads:
    input:
        reads=lambda wc: config[wc.hap],
    output:
        fasta="{temp}/{hap}.fasta",
    conda:
        "envs/env.yaml"
    shell:
        """
        if [[ {input.reads} =~ .*\.(fofn) ]]; then
                cat $(cat {input.reads}) | seqtk seq -l 80 -A > {output.fasta}
        elif [[ {input.reads} =~ .*\.(fa|fq|fasta|fastq|gz) ]]; then
                cat {input.reads} | seqtk seq -l 80 -A > {output.fasta}
        fi
        """


rule run_trio_binning:
    input:
        fasta=expand(
            rules.collect_reads.output.fasta, temp=config["temp"], hap="reads"
        ),
        mat=expand(rules.collect_reads.output.fasta, temp=config["temp"], hap="pat"),
        pat=expand(rules.collect_reads.output.fasta, temp=config["temp"], hap="mat"),
    output:
        mat=expand(
            "{temp}/trio/haplotype/haplotype-mat.fasta.gz", temp=config["temp"]
        ),
        pat=expand(
            "{temp}/trio/haplotype/haplotype-pat.fasta.gz", temp=config["temp"]
        ),
        unk=expand(
            "{temp}/trio/haplotype/haplotype-unknown.fasta.gz", temp=config["temp"]
        ),
        trio=temp(directory(config["temp"] + "/trio")),
    params:
        tech=config["tech"],
        tmp=config["temp"],
        genomeSize=config["genomeSize"],
    conda:
        "envs/env.yaml"
    threads: config["threads"]
    shell:
        """
        which canu
        canu --version
        canu -haplotype \
            maxThreads={threads} \
            useGrid=false \
            -p asm -d {params.tmp}/trio \
            -genomeSize={params.genomeSize} \
            -haplotypemat {input.mat} \
            -haplotypepat {input.pat} \
            -{params.tech} {input.fasta}
        """


rule move_reads:
    input:
        mat=rules.run_trio_binning.output.mat,
        pat=rules.run_trio_binning.output.pat,
        unk=rules.run_trio_binning.output.unk,
        trio=rules.run_trio_binning.output.trio,
    output:
        mat="results/mat.fa.gz",
        pat="results/pat.fa.gz",
        unk="results/unk.fa.gz",
    conda:
        "envs/env.yaml"
    shell:
        """
        cp {input.mat} {output.mat}
        cp {input.pat} {output.pat}
        cp {input.unk} {output.unk}
        """
rule read_stats:
    input:
        mat=rules.run_trio_binning.output.mat,
        pat=rules.run_trio_binning.output.pat,
        unk=rules.run_trio_binning.output.unk,
    output:
        "results/phasing.stats.tbl"
    conda:
        "envs/env.yaml"
    threads:1
    shell:
        "{SDIR}/scripts/phasing_by_lengths.py {input} > {output}"

rule all:
    input:
        rules.move_reads.output,
        rules.read_stats.output,
        trio=rules.run_trio_binning.output.trio,
